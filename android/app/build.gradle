plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'app.alchan'
    compileSdk 34

    defaultConfig {
        applicationId "app.alchan"
        minSdk 26
        targetSdk 34
        versionCode (System.getenv("VERSION_CODE") ?: "1").toInteger()
        versionName System.getenv("VERSION_NAME") ?: "1.0.0"
    }

    signingConfigs {
        release {
            def keystorePath = System.getenv("KEYSTORE_PATH") ?: ""
            if (keystorePath) {
                storeFile file(keystorePath)
                storePassword System.getenv("KEYSTORE_PASSWORD") ?: ""
                keyAlias System.getenv("KEY_ALIAS") ?: ""
                keyPassword System.getenv("KEY_PASSWORD") ?: ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            def keystorePath = System.getenv("KEYSTORE_PATH") ?: ""
            if (keystorePath) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
}
